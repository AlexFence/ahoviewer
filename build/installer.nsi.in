!define AppName "@PACKAGE@"
!define DisplayName "${AppName} ${ReleaseVersion} (${Bits})"
!define Author "ahoka"
!define Homepage "@PACKAGE_URL@"
!define ReleaseVersion "@VERSION@"
!define ReleaseNotes "${Homepage}/releases/${ReleaseVersion}"

!define AppDir "${AppName}"
!define AppFile "${AppName}.exe"
!define InstallerFile "${AppName}-v${ReleaseVersion}-${Arch}-installer.exe"
!define UninstallerFile "Uninstall ${AppName}.exe"

!define RegKeyInstall "SOFTWARE\${AppName}"
!define RegKeyUninstall "SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\${AppName}"

!define KillCmd 'cmd /c taskkill /f /im "${AppFile}"'

RequestExecutionLevel admin

# Modules
!include "MUI2.nsh"
!include "LogicLib.nsh"
!include "FileFunc.nsh"
!include "x64.nsh"
!include "FileAssociation.nsh"

# Installer/Uninstaller params
Name "${AppName}"
Caption "${DisplayName}"
BrandingText "${DisplayName}"
ShowInstDetails show
ShowUninstDetails show
InstallDir ""
InstallDirRegKey HKLM "${RegKeyInstall}" "InstallPath"

# Package params
CRCCheck on
SetCompressor /SOLID lzma

# NSIS Installer output dir
OutFile "${InstallerFile}"

# Metadata params
VIProductVersion "${ReleaseVersion}.0"
VIAddVersionKey "ProductName" "${DisplayName}"
VIAddVersionKey "ProductVersion" "${ReleaseVersion}"
VIAddVersionKey "FileDescription" "${DisplayName}"
VIAddVersionKey "FileVersion" "${ReleaseVersion}"
VIAddVersionKey "LegalCopyright" "${Author}"

# GUI
!define MUI_ICON "..\src\ahoviewer.ico"
!define MUI_WELCOMEPAGE_TITLE "${DisplayName}"
!define MUI_WELCOMEPAGE_TITLE_3LINES
!define MUI_ABORTWARNING
!define MUI_ABORTWARNING_CANCEL_DEFAULT
!define MUI_FINISHPAGE_NOAUTOCLOSE
!define MUI_FINISHPAGE_RUN_TEXT "Launch ${AppName}"
!define MUI_FINISHPAGE_RUN "$INSTDIR\${AppFile}"
!define MUI_FINISHPAGE_NOREBOOTSUPPORT
!define MUI_FINISHPAGE_SHOWREADME "${ReleaseNotes}"
!define MUI_FINISHPAGE_SHOWREADME_TEXT "Open release notes in web browser"
!define MUI_UNICON "${NSISDIR}/Contrib/Graphics/Icons/modern-uninstall.ico"
!define MUI_UNFINISHPAGE_NOAUTOCLOSE
!insertmacro MUI_PAGE_WELCOME
!insertmacro MUI_PAGE_DIRECTORY
!insertmacro MUI_PAGE_COMPONENTS
!insertmacro MUI_PAGE_INSTFILES
!insertmacro MUI_PAGE_FINISH
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH
!insertmacro MUI_LANGUAGE "English"

# Installer
Section "${DisplayName}" SEC_DEFAULT
    DetailPrint '${KillCmd}'
    nsExec::Exec /TIMEOUT=3000 '${KillCmd}'

    SetOutPath $INSTDIR
    File /r "${AppName}-${ReleaseVersion}-${Arch}\*"

    WriteUninstaller "$INSTDIR\${UninstallerFile}"

    WriteRegStr HKLM "${RegKeyInstall}" "InstallPath" "$INSTDIR"
    WriteRegStr HKLM "${RegKeyUninstall}" "DisplayName" "${DisplayName}"
    WriteRegStr HKLM "${RegKeyUninstall}" "UninstallString" "$INSTDIR\${UninstallerFile}"
    WriteRegStr HKLM "${RegKeyUninstall}" "DisplayIcon" "$INSTDIR\${AppFile}"
    WriteRegStr HKLM "${RegKeyUninstall}" "DisplayVersion" "${ReleaseVersion}"
    WriteRegStr HKLM "${RegKeyUninstall}" "Publisher" "${Author}"
    WriteRegStr HKLM "${RegKeyUninstall}" "URLInfoAbout" "${Homepage}/issues"
    WriteRegStr HKLM "${RegKeyUninstall}" "HelpLink" "${Homepage}/issues"

    ${GetSize} "$INSTDIR" "/S=0K" $0 $1 $2
    IntFmt $0 "0x%08X" $0
    WriteRegDWORD HKLM "${RegKeyUninstall}" "EstimatedSize" "$0"

    !insertmacro UPDATEFILEASSOC
SectionEnd

# Components
Section "Create Desktop shortcut" SEC_SHORTCUT_DESKTOP
    ${If} ${SectionIsSelected} ${SEC_SHORTCUT_DESKTOP}
        SetOutPath $INSTDIR
        CreateShortCut "$DESKTOP\${AppName}.lnk" "$INSTDIR\${AppFile}"
    ${EndIf}
SectionEnd

Section "Create Start Menu shortcut" SEC_SHORTCUT_START
    ${If} ${SectionIsSelected} ${SEC_SHORTCUT_START}
        SetOutPath $INSTDIR
        CreateShortCut "$SMPROGRAMS\${AppName}.lnk" "$INSTDIR\${AppFile}"
    ${EndIf}
SectionEnd

SectionGroup "File Associations"
    Section ".gif" SEC_FILE_GIF
        ${If} ${SectionIsSelected} ${SEC_FILE_GIF}
            !insertmacro APP_ASSOCIATE "gif" "${AppName}.AssocFile.GIF" \
                "GIF Image File" "" \
                "&Open with ${DisplayName}" '"$INSTDIR\${AppFile}" "%1"'
        ${EndIf}
    SectionEnd
    Section ".jpg" SEC_FILE_JPG
        ${If} ${SectionIsSelected} ${SEC_FILE_JPG}
            !insertmacro APP_ASSOCIATE "jpe" "${AppName}.AssocFile.JPE" \
                "JPEG Image File" "" \
                "&Open with ${DisplayName}" '"$INSTDIR\${AppFile}" "%1"'
            !insertmacro APP_ASSOCIATE "jpg" "${AppName}.AssocFile.JPG" \
                "JPEG Image File" "" \
                "&Open with ${DisplayName}" '"$INSTDIR\${AppFile}" "%1"'
            !insertmacro APP_ASSOCIATE "jpeg" "${AppName}.AssocFile.JPEG" \
                "JPEG Image File" "" \
                "&Open with ${DisplayName}" '"$INSTDIR\${AppFile}" "%1"'
        ${EndIf}
    SectionEnd
    Section ".png" SEC_FILE_PNG
        ${If} ${SectionIsSelected} ${SEC_FILE_PNG}
            !insertmacro APP_ASSOCIATE "png" "${AppName}.AssocFile.PNG" \
                "PNG Image File" "" \
                "&Open with ${DisplayName}" '"$INSTDIR\${AppFile}" "%1"'
        ${EndIf}
    SectionEnd
    Section ".webm" SEC_FILE_WEBM
        ${If} ${SectionIsSelected} ${SEC_FILE_WEBM}
            !insertmacro APP_ASSOCIATE "webm" "${AppName}.AssocFile.WEBM" \
                "WebM Video File" "" \
                "&Open with ${DisplayName}" '"$INSTDIR\${AppFile}" "%1"'
        ${EndIf}
    SectionEnd
    Section ".rar" SEC_FILE_RAR
        ${If} ${SectionIsSelected} ${SEC_FILE_RAR}
            !insertmacro APP_ASSOCIATE "rar" "${AppName}.AssocFile.RAR" \
                "RAR Archive" "" \
                "&Open with ${DisplayName}" '"$INSTDIR\${AppFile}" "%1"'
        ${EndIf}
    SectionEnd
    Section ".cbr" SEC_FILE_CBR
        ${If} ${SectionIsSelected} ${SEC_FILE_CBR}
            !insertmacro APP_ASSOCIATE "cbr" "${AppName}.AssocFile.CBR" \
                "Comic Book RAR Archive" "$INSTDIR\${AppFile},0" \
                "&Open with ${DisplayName}" '"$INSTDIR\${AppFile}" "%1"'
        ${EndIf}
    SectionEnd
    Section ".zip" SEC_FILE_ZIP
        ${If} ${SectionIsSelected} ${SEC_FILE_ZIP}
            !insertmacro APP_ASSOCIATE "zip" "${AppName}.AssocFile.ZIP" \
                "Zip Archive" "" \
                "&Open with ${DisplayName}" '"$INSTDIR\${AppFile}" "%1"'
        ${EndIf}
    SectionEnd
    Section ".cbz" SEC_FILE_CBZ
        ${If} ${SectionIsSelected} ${SEC_FILE_CBZ}
            !insertmacro APP_ASSOCIATE "cbz" "${AppName}.AssocFile.CBZ" \
                "Comic Book Zip Archive" "$INSTDIR\${AppFile},0" \
                "&Open with ${DisplayName}" '"$INSTDIR\${AppFile}" "%1"'
        ${EndIf}
    SectionEnd
SectionGroupEnd

# Uninstaller
Section "Uninstall"
    DetailPrint '${KillCmd}'
    nsExec::Exec /TIMEOUT=3000 '${KillCmd}'

    Delete "$INSTDIR\${UninstallerFile}"
    RMDir /r "$INSTDIR"

    Delete "$DESKTOP\${AppName}.lnk"
    Delete "$SMPROGRAMS\${AppName}.lnk"

    !insertmacro APP_UNASSOCIATE "gif"  "${AppName}.AssocFile.GIF"
    !insertmacro APP_UNASSOCIATE "jpe"  "${AppName}.AssocFile.JPE"
    !insertmacro APP_UNASSOCIATE "jpg"  "${AppName}.AssocFile.JPG"
    !insertmacro APP_UNASSOCIATE "jpeg" "${AppName}.AssocFile.JPEG"
    !insertmacro APP_UNASSOCIATE "png"  "${AppName}.AssocFile.PNG"
    !insertmacro APP_UNASSOCIATE "webm" "${AppName}.AssocFile.WEBM"
    !insertmacro APP_UNASSOCIATE "rar"  "${AppName}.AssocFile.RAR"
    !insertmacro APP_UNASSOCIATE "cbr"  "${AppName}.AssocFile.CBR"
    !insertmacro APP_UNASSOCIATE "zip"  "${AppName}.AssocFile.ZIP"
    !insertmacro APP_UNASSOCIATE "cbz"  "${AppName}.AssocFile.CBZ"

    DeleteRegKey HKLM "${RegKeyInstall}"
    DeleteRegKey HKLM "${RegKeyUninstall}"

    !insertmacro UPDATEFILEASSOC
SectionEnd

# Functions
Function .onInit
    # set default section as read only
    SectionSetFlags ${SEC_DEFAULT} 17

    ${If} $InstDir == ""
        ${If} ${RunningX64}
        ${AndIf} "${Arch}" == "win64"
            StrCpy $InstDir "$PROGRAMFILES64\${AppDir}\"
        ${Else}
            StrCpy $InstDir "$PROGRAMFILES32\${AppDir}\"
        ${EndIf}
    ${EndIf}
FunctionEnd
